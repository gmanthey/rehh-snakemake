import os

localrules: all, combine_chromosomes, population_chromosome_vcf
configfile: "config.yml"

def get_populations():
    populations = {}
    with open(config['populations_file']) as f:
        for line in f:
            line = line.strip().split()
            if line[0] not in populations:
                populations[line[0]] = []
            populations[line[0]].extend(line[1:])
    
    return populations

def get_chromosomes():
    chromosomes = []
    with open(config["chromosome_file"], 'r') as f:
        for line in f:
            line = line.strip()
            chromosomes.append(line)

    return chromosomes

def cross_populations():
    populations = list(get_populations().keys())

    pop_files = expand("results/ihs/combined/{population}.csv", population=populations)

    if len(populations) == 1:
        return pop_files

    combined_files = []

    for i, pop1 in enumerate(populations[:-1]):
        for j, pop2 in enumerate(populations[i+1:]):
            combined_files.append(f"results/xpehh/combined/{pop1}-{pop2}.csv")

    return pop_files + combined_files

rule all:
    input:
        cross_populations()

rule population_chromosome_vcf:
    input:
        config["vcf_file"]
    output:
        "results/vcf/{population}/by-chromosome/{chromosome}.vcf.gz"
    log:
        "log/vcf/{population}/{chromosome}.log"
    run:
        individuals = ','.join(get_populations()[wildcards.population])
        shell(f"bcftools view -s {individuals} -r {wildcards.chromosome} -Oz -o {output} {input} > {log} 2>&1")

rule scan_hh_ihs:
    input:
        "results/vcf/{population}/by-chromosome/{chromosome}.vcf.gz"
    output:
        "results/ihh/{population}/by-chromosome/{chromosome}.Rdata",
        "results/ihs/{population}/by-chromosome/{chromosome}.csv",
        "results/ihs/{population}/by-chromosome/{chromosome}.f-class.csv"
    script:
        "scripts/scan_hh_ihs.R"

rule xpehh:
    input:
        "results/ihh/{pop1}/by-chromosome/{chromosome}.Rdata",
        "results/ihh/{pop2}/by-chromosome/{chromosome}.Rdata"
    output:
        "results/xpehh/{pop1}-{pop2}/by-chromosome/{chromosome}.csv"
    script:
        "scripts/xpehh.R"

rule combine_chromosomes:
    input:
        expand("results/{{type}}/{{population}}/by-chromosome/{chromosome}.csv", chromosome = get_chromosomes())
    output:
        "results/{type}/combined/{population}.csv"
    script:
        "scripts/combine_files.py"