import os

localrules: all, combine_chromosomes, population_chromosome_vcf
configfile: "config.yml"

def get_populations():
    populations = {}
    with open(config['populations_file']) as f:
        for line in f:
            line = line.strip().split()
            if line[0] not in populations:
                populations[line[0]] = []
            populations[line[0]].extend(line[1:])
    
    return populations

def get_chromosomes():
    chromosomes = []
    with open(config["chromosome_file"], 'r') as f:
        for line in f:
            line = line.strip()
            chromosomes.append(line)

    return chromosomes

def cross_populations():
    populations = list(get_populations().keys())

    pop_files = expand("results/ihs/{population}.csv", population=populations)

    if len(populations) == 1:
        return pop_files

    combined_files = []

    for i, pop1 in enumerate(populations[:-1]):
        for j, pop2 in enumerate(populations[i+1:]):
            combined_files.append(f"results/xpehh/{pop1}-{pop2}.csv")

    return pop_files + combined_files

rule all:
    input:
        cross_populations()

rule population_chromosome_vcf:
    input:
        config["vcf_file"],
        config["populations_file"]
    output:
        "results/vcf/{population}/by-chromosome/{chromosome}.vcf.gz"
    log:
        "log/vcf/{population}/{chromosome}.log"
    run:
        individuals = ','.join(get_populations()[wildcards.population])
        shell(f"bcftools view -s {individuals} -r {wildcards.chromosome} -Oz -o {output} {input[0]} > {log} 2>&1")

rule scan_hh:
    input:
        "results/vcf/{population}/by-chromosome/{chromosome}.vcf.gz"
    output:
        "results/ihh/{population}/by-chromosome/{chromosome}.Rdata",
    script:
        "scripts/scan_hh.R"

rule ihs:
    input:
        expand("results/ihh/{{population}}/by-chromosome/{chromosome}.Rdata", chromosome=get_chromosomes())
    output:
        "results/ihs/{population}.csv",
        "results/ihs/{population}.f_class.csv"
    script:
        "scripts/ihs.R"

rule xpehh:
    input:
        expand("results/ihh/{{pop1}}/by-chromosome/{chromosome}.Rdata", chromosome=get_chromosomes()),
        expand("results/ihh/{{pop2}}/by-chromosome/{chromosome}.Rdata", chromosome=get_chromosomes())
    output:
        "results/xpehh/{pop1}-{pop2}.csv",
        "results/xpehh/{pop1}-{pop2}.candidates.csv",
        "results/xpehh/{pop1}-{pop2}.normalized.csv",
    script:
        "scripts/xpehh.R"
